apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.aefyr.sai"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 52
        versionName "3.10"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        normal {
            dimension "version"
            buildConfigField "int", "DEFAULT_THEME", "0"
            buildConfigField "boolean", "IS_FLOSS_BUILD", "false"
        }

        fdroid {
            dimension "version"
            applicationId "com.aefyr.sai.fdroid"
            buildConfigField "int", "DEFAULT_THEME", "6"
            buildConfigField "boolean", "IS_FLOSS_BUILD", "true"
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.2.0-alpha05'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'moe.shizuku.privilege:api:3.1.0'
    implementation 'com.google.android:flexbox:2.0.1'
    implementation 'com.tomergoldst.android:tooltips:1.0.11'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    implementation 'com.github.aefyr.flexfilter:flexfilter:0.9-alpha'
    implementation 'com.github.aefyr:pseudoapksigner:1.6'

    //Google services
    normalImplementation 'com.google.firebase:firebase-analytics:17.3.0'
    normalImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    normalImplementation 'com.android.billingclient:billing:2.2.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.1'
}

//Cancel google services/fabric plugins for fdroid flavor
gradle.taskGraph.beforeTask {
    String taskName = it.name.toLowerCase()
    if (taskName.contains("fdroid") && (taskName.contains("googleservices") || taskName.contains("fabric"))) {
        println(String.format("Cancelling task \"%s\" due to fdroid build flavor", it.name))
        it.setEnabled(false)
    }
}

apply plugin: 'com.google.gms.google-services'
